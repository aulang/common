apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                afterEvaluate {
                    name = project.description
                    description = project.description
                }
            }
            versionMapping {
                usage('java-api') {
                    fromResolutionResult()
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        maven {
            def snapshotsRepoUrl = "https://$maven_registry/maven/repository/2110124-snapshot-DoCVfz/"
            def releasesRepoUrl = "https://$maven_registry/maven/repository/2110124-release-FYMnre/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username "$maven_registry_username"
                password "$maven_registry_password"
            }
        }
    }
}

configureDeploymentRepository(project)

static void configureDeploymentRepository(Project project) {
    project.plugins.withType(MavenPublishPlugin.class).configureEach {
        PublishingExtension publishing = project.getExtensions().getByType(PublishingExtension.class)
        if (project.hasProperty('deploymentRepository')) {
            publishing.repositories.maven {
                it.url = project.property('deploymentRepository')
                it.name = 'deployment'
            }
        }
    }
}