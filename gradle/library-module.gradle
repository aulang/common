apply plugin: 'java-library'

apply from: "$rootDir/gradle/publications.gradle"

dependencies {
}

jar {
    manifest.attributes['Implementation-Title'] = project.name
    manifest.attributes['Implementation-Version'] = project.version
    manifest.attributes['Automatic-Module-Name'] = project.name.replace('-', '.')
    manifest.attributes['Created-By'] = "${System.getProperty('java.version')} (${System.getProperty('java.specification.vendor')})"
}

normalization {
    runtimeClasspath {
        ignore 'META-INF/MANIFEST.MF'
    }
}

javadoc {
    options.encoding = 'UTF-8'
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.author = true
    options.header = project.name
    options.use = true

    options.addStringOption('Xdoclint:none', '-quiet')

    logging.captureStandardError LogLevel.INFO
    logging.captureStandardOutput LogLevel.INFO
}

task sourcesJar(type: Jar, dependsOn: classes) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact javadocJar
        }
    }
}